plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

repositories {
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
}

sourceCompatibility = 17
targetCompatibility = 17

ext.Versions = new Properties()
Versions.load(file("mcversion-1.19.4.properties").newReader())

archivesBaseName = "GBfabrictools"

// No, we don't want that in this case, as we want to be able to develop a new
// version while leaving all current mods on the old version.
// version = "${Versions['gbfabrictools_version']}"
version = "1.3.5+1.19.4"

loom {
	mixin.defaultRefmapName = "de-guntram-mcmod-fabrictools-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "com.terraformersmc:modmenu:${Versions['modmenu_version']}"
}



java {
    withSourcesJar()
}

jar {
	from "LICENSE"
}

group = "de.guntram.mcmod"
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                // builtBy remapJar
            }
        }
    }
    repositories {
        maven {
            url = "file://tmp/mymavenrepo"
        }
    }
}

task mypublish(dependsOn: publish, type: Exec) {
    commandLine "rsync", "-av", "/tmp/mymavenrepo/", "maven@minecraft.guntram.de:/var/www/html/maven/"
}
